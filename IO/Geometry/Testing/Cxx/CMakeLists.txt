# Tests with data
if(VTK_DATA_ROOT)
  set(NEEDS_DATA
  TestProStarReader.cxx
  TestTecplotReader.cxx
  TestAMRReadWrite.cxx
  )
endif()

create_test_sourcelist(Tests ${vtk-module}CxxTests.cxx
  ${NEEDS_DATA}
  TestSimplePointsReaderWriter.cxx
  TestWindBladeReader.cxx
  EXTRA_INCLUDE vtkTestDriver.h
)

vtk_module_test_executable(${vtk-module}CxxTests ${Tests})

set(TestsToRun ${Tests})
list(REMOVE_ITEM TestsToRun ${vtk-module}CxxTests.cxx TestWindBladeReader.cxx)

# Add all the executables
foreach(test ${TestsToRun})
  get_filename_component(TName ${test} NAME_WE)
  if(VTK_DATA_ROOT)
    add_test(NAME ${vtk-module}Cxx-${TName}
      COMMAND ${vtk-module}CxxTests ${TName}
        -D ${VTK_DATA_ROOT}
        -T ${VTK_TEST_OUTPUT_DIR}
        -V Baseline/IO/${TName}.png)
  else()
    add_test(NAME ${vtk-module}Cxx-${TName}
      COMMAND ${vtk-module}CxxTests ${TName})
  endif()
endforeach()

if(VTK_LARGE_DATA_ROOT AND NOT CMAKE_WORDS_BIGENDIAN)
  add_test(NAME ${vtk-module}Cxx-TestWindBladeReader
    COMMAND ${vtk-module}CxxTests TestWindBladeReader
    -D ${VTK_LARGE_DATA_ROOT}
    -T ${VTK_TEST_OUTPUT_DIR}
    -V Baseline/Parallel/TestWindBladeReader.png)
endif()
